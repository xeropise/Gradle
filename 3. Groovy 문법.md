### Groovy 문법

- 자세한 것은 [Groovy 공홈](https://groovy-lang.org/syntax.html) 을 참조하고, 간단한 것만 서술



- 문자열

  - 작은 따옴표('')

    - 자바의 문자열과 거의 같은 용도, 자바의 String 클래스 사용

      

  - 큰 따옴표("")

    - 문자열 내부에 $ 기호로 동적인 내용을 넣을 수 있음, 내부적으로 그루비의 GString 클래스 사용

```groovy
// 작은 따옴표
String name = 'john'

// 큰 따옴표
String title = "Project: ${project.name}"
// 큰 따옴표 안의 중괄호는 생략 가능하나 프로퍼티나 메소드 접근 여부에 차이
String title = "Prject: $name"

// 작은 따옴표로 문자열 여러 줄 표시
String greet = ```**************
  * Hello *
  ******************```

// 큰 따옴표로 문자열 여러 줄 표기
String banner = """######################################
 ${new Date().toGMTString()}
###################################"""

```



<br>



- 메서드 호출 시 괄호 생략

```groovy
println('Hello World!')
println 'Hello World'

// 인수가 여러 개일 때도 괄호 생략가능
printf '%5.3f', 345.0

// 인수가 없을 때는 괄호 생략 불가
println   <= (X) 
println() <= (O)

// 다음의 경우도 괄호 생략 불가
println someMethod 'foo'	<= (X)
println someMethod('foo')	<= (O)

// 메서드 호출이 식의 오른쪽에 있을 때
def status = someMethod 'foo'		<= (X)
def status = someMethod('foo')	<= (O)
```



<br>



- def를 이용한 형 지정 생략

  - 자바처럼 변수나 인수에 형을 지정할 수도 있지만 생략도 가능, 형 지정을 생략할 때는 def 키워드 이용

    

  - 변수뿐만 아니라 메서드의 인수나 반환 값에도 적용 가능, 메서드 인수의 형을 생략하면 알아서 def로 간주

    

  - def를 지정하는 것은 자바에서 Object 형을 지정한 것과 같음

    - 단, 그루비에서는 덕 타이핑 기능이 있어 def일 때도 객체의 메서드나 속성을 모두 참조 가능
    - 자세한 것은 [여기](https://www.baeldung.com/groovy-def-keyword) 를 참조

```groovy
// 형을 지정한 변수 정의
String name = 'John'

// 형을 생략한 변수 정의 
def name = 'John'

// 리턴타입이 def, void 형도 받을 수 잇음
def multiply(x, y) {
  return x*y;
}

// 자바에서 다음 코드를 컴파일 하면 오류 
Object name = "John";
System.out.println(name.toUpperCase());

// 그루비에서는 문제가 없음
def name = 'John'
println name.toUpperCase()
```



<br>





- Lists

```groovy
// lists, JDK java.util.List 이다. list를 정의하면 기본적으로 java.util.ArrayList 를 사용한다.
def numbers = [1, 2, 3]
assert numbers instanceof List
assert numbers.size() == 3

// type이 다른 list도 만들 수 있다.
def heterogeneous = [1. "a", true]

// 다른 backing type의 list 를 사용할 수 있다.
def arrayList = [1, 2, 3]
assert arrayList instanceof java.util.ArrayList

def linkedList = [2, 3, 4] as LinkedList    
assert linkedList instanceof java.util.LinkedList

LinkedList otherLinked = [3, 4, 5]          
assert otherLinked instanceof java.util.LinkedList

// 아래와 같은 방법으로 list에 접근하거나 elements를 추가할 수 있다.
def letters = ['a', 'b', 'c', 'd']

assert letters[0] == 'a'     
assert letters[1] == 'b'

// 음수를 사용하는 경우, 뒤에서 부터 접근한다. -1의 경우 맨 뒤에서 첫번째
assert letters[-1] == 'd'    
assert letters[-2] == 'c'

letters[2] = 'C'             
assert letters[2] == 'C'

// list의 맨 끝에 element 를 추가 한다.
letters << 'e'               
assert letters[ 4] == 'e'
assert letters[-1] == 'e'

// 한번에 2개를 접근하거나, range Operator로 갯수만큼 접근 가능하다.
assert letters[1, 3] == ['b', 'd']         
assert letters[2..4] == ['C', 'd', 'e']

// 다차원으로도 정의 가능하다.
def multi = [[0,1], [2, 3]]
assert multi[1][0] == 2

```



- Arrays (배열)

```groovy
// 특정 type만 지정하여 받을 수 있다.
String[] arrStr = ['Ananas', 'Banana', 'Kiwi']  

assert arrStr instanceof String[]    
assert !(arrStr instanceof List)

// 이 방법으로도 생성 가능
def numArr = [1, 2, 3] as int[]      

assert numArr instanceof int[]       
assert numArr.size() == 3

// 다차원으로도 정의 가능
def matrix3 = new Integer[3][3]         
assert matrix3.size() == 3

Integer[][] matrix2                     
matrix2 = [[1, 2], [3, 4]]
assert matrix2 instanceof Integer[][]

// 자바 스타일로도 초기화 가능
def primes = new int[] {2, 3, 5, 7, 11}
assert primes.size() == 5 && primes.sum() == 28
assert primes.class.name == '[I'

def pets = new String[] {'cat', 'dog'}
assert pets.size() == 2 && pets.sum() == 'catdog'
assert pets.class.name == '[Ljava.lang.String;'

// traditional Groovy alternative still supported
String[] groovyBooks = [ 'Groovy in Action', 'Making Java Groovy' ]
assert groovyBooks.every{ it.contains('Groovy') }
```



- Maps

```groovy
// map 은 이렇게 정의, java.util.LinkedHashMap로 만든다.
def colors = [red: '#FF0000', green: '#00FF00', blue: '#0000FF']   

// 두가지 방법으로 접근 가능
assert colors['red'] == '#FF0000'    
assert colors.green  == '#00FF00'    

// 추가는 다음과 같이 가능
colors['pink'] = '#FF00FF'           
colors.yellow  = '#FFFF00'           


assert colors.pink == '#FF00FF'
assert colors['yellow'] == '#FFFF00'

assert colors instanceof java.util.LinkedHashMap

// 없는 값을 접근하려고 하면 null 을 받는다.
assert colors.unknown == null

// 다른 타입을 key로 사용하려는 경우, number는 가능
def numbers = [1: 'one', 2: 'two']

assert numbers[1] == 'one'

// 변수를 key에 통과시키려는 경우 결과가 다르다.
def key = 'name'
def person = [key: 'Guillaume']      

assert !person.containsKey('name')   
assert person.containsKey('key')   

// 변수를 통과시키려면 다음과 같이 사용해야 한다.
person = [(key): 'Guillaume']        

assert person.containsKey('name')    
assert !person.containsKey('key')    

```



<br>



- 클로저

  - 자바8의 람다나, 다른 프로그래밍 언어의 함수 객체 정도로 이해

    

  - 자세한 것은 [여기](https://groovy-lang.org/closures.html) 를 참조

    

  - 중괄호 {} 를 이요해서 정의, <클로저명>.call() 또는 일반 메서드 호출처럼 <클로저명>() 으로 실행

```groovy
// 클로저 정의
def clos1 = { mes -> println "Hi, $mes"}

// 인수를 생략하면 암묵적 인수인 it을 사용
def clos2 = { println "Hi, $it"}

// 클로저 실행
clos1.call('John') // Hi, John
clos2('Bob') // Hi, Bob

// 메서드 호출과 마찬가지로 괄호 생략 가능
clos2 'Alice2' // Hi, Alice

// 태스크 정의는 클로저로 구현, {}로 감싼 부분이 실제로는 클로저
task hello << {
  println 'Hello Gradle World!'
}

// 사전에 정의한 클로저를 이용해서 동일한 태스크를 정의할 때는 다음과 같이 실행

// 태스크에 추가할 처리를 클로저로 정의
def clos = { println 'Hello Gradle world!' }

// 태스크 정의, 테스크 정의 방법은 많으니 하단의 링크 참조
task hello {}

// 태스크에 클로저 추가, <<는 leftShift()의 생략형이므로 아래와 같음
hello.leftShift(clos)
```



> [태스크 정의 방법](https://docs.gradle.org/current/javadoc/org/gradle/api/Task.html)

